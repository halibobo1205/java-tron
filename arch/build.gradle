import org.gradle.nativeplatform.platform.internal.Architectures

description = "arch â€“ a distributed consensus arithmetic for blockchain."


static def isX86() {
    def arch = System.getProperty("os.arch").toLowerCase()
    return Architectures.X86_64.isAlias(arch) || Architectures.X86.isAlias(arch)
}

static def isArm64() {
    def arch = System.getProperty("os.arch").toLowerCase()
    return new Architectures.KnownArchitecture("arm64", "aarch64").isAlias(arch)
}

if (isArm64() && !JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
    throw new GradleException("Java 17 or later is required to build Java-Tron for arm64.\n" +
            "  Detected version ${JavaVersion.current()}")
}

if (isX86() && !JavaVersion.current().isJava8()) {
    throw new GradleException("Java 8 is required to build Java-Tron for x86.\n" +
            "  Detected version ${JavaVersion.current()}")
}

if (isX86()) {
    ext {
        leveldbGroup = "org.fusesource.leveldbjni"
        leveldbName = "leveldbjni-all"
        leveldbVersion = "1.8"
        rocksDBVersion = "5.15.10"
    }
} else {
    ext {
        leveldbGroup = "com.halibobor"
        leveldbName = "leveldbjni-all"
        leveldbVersion = "1.18.3"
        rocksDBVersion = "7.7.3"
    }
}

sourceSets {
    x86 {
        java {
            srcDir 'src/main/java/x86'
        }
    }
    arm {
        java {
            srcDir 'src/main/java/arm'
        }
    }
}

dependencies {
    api group: leveldbGroup, name: leveldbName, version: leveldbVersion
    api group: 'org.rocksdb', name: 'rocksdbjni', version: rocksDBVersion
}

tasks.withType(JavaCompile).configureEach {
    source = isX86() ? sourceSets.x86.java : sourceSets.arm.java
}
