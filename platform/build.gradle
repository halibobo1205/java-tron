import org.gradle.nativeplatform.platform.internal.Architectures

description = "platform â€“ a distributed consensus arithmetic for blockchain."


static def isX86() {
    def arch = System.getProperty("os.arch").toLowerCase()
    return Architectures.X86_64.isAlias(arch) || Architectures.X86.isAlias(arch)
}

static def isArm64() {
    def arch = System.getProperty("os.arch").toLowerCase()
    return Architectures.AARCH64.isAlias(arch)
}

if (isX86()) {
    ext {
        rocksDBVersion = "5.15.10"
    }
} else if (isArm64()) {
    ext {
        rocksDBVersion = "7.7.3"
    }
} else {
    throw new GradleException("Unsupported architecture: ${System.getProperty("os.arch")}")
}

sourceSets {
    x86 {
        java {
            srcDirs 'src/main/java/x86', 'src/main/java/common'
        }
    }
    arm {
        java {
            srcDirs 'src/main/java/arm', 'src/main/java/common'
        }
    }
}

dependencies {
    api group: 'org.fusesource.leveldbjni', name: 'leveldbjni-all', version: '1.8'
    api group: 'org.rocksdb', name: 'rocksdbjni', version: rocksDBVersion
    api group: 'commons-io', name: 'commons-io', version: '2.18.0'
    api 'io.github.tronprotocol:zksnark-java-sdk:1.0.0' exclude(group: 'commons-io', module: 'commons-io')
}

tasks.withType(JavaCompile).configureEach {
    source = isX86() ? sourceSets.x86.java : sourceSets.arm.java
}
