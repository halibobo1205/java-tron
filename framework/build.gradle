plugins {
    id "org.gradle.test-retry" version "1.5.9"
    id "org.sonarqube" version "2.6"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

gitProperties.failOnNoGitDirectory = false;

apply plugin: 'application'
apply plugin: 'checkstyle'

mainClassName = 'org.tron.program.FullNode'

def versions = [
        checkstyle: '8.7',
]



configurations {
    checkstyleConfig

}

configurations.getByName('checkstyleConfig') {
    transitive = false
}

static def isWindows() {
    return org.gradle.internal.os.OperatingSystem.current().isWindows()
}

task version(type: Exec) {
    commandLine 'bash', '-c', '../ver.sh'
}

dependencies {
    //local libraries
    implementation fileTree(dir: 'libs', include: '*.jar')
    // end local libraries
    implementation group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.2'
    implementation group: 'com.github.davidb', name: 'metrics-influxdb', version: '0.8.2'
    // http
    implementation 'org.eclipse.jetty:jetty-server:9.4.57.v20241219'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.57.v20241219'
    implementation 'com.alibaba:fastjson:1.2.83'
    implementation group: 'io.perfmark', name: 'perfmark-api', version: '0.27.0'
    // end http

    // https://mvnrepository.com/artifact/com.github.briandilley.jsonrpc4j/jsonrpc4j
    implementation group: 'com.github.briandilley.jsonrpc4j', name: 'jsonrpc4j', version: '1.6'

    // https://mvnrepository.com/artifact/javax.portlet/portlet-api
    compileOnly group: 'javax.portlet', name: 'portlet-api', version: '3.0.1'

    implementation (group: 'org.pf4j', name: 'pf4j', version: '3.10.0') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    testImplementation group: 'org.springframework', name: 'spring-test', version: "${springVersion}"
    implementation group: 'org.zeromq', name: 'jeromq', version: '0.5.3'
    api project(":chainbase")
    api project(":protocol")
    api project(":actuator")
    api project(":consensus")
}

check.dependsOn 'lint'

checkstyle {
    toolVersion = "${versions.checkstyle}"
    configFile = file("config/checkstyle/checkStyleAll.xml")
}


checkstyleMain {
    source = 'src/main/java'
}

task lint(type: Checkstyle) {
    // Cleaning the old log because of the creation of the new ones (not sure if totaly needed)
    delete fileTree(dir: "${project.rootDir}/app/build/reports")
    source 'src'
    include '**/*.java'
    exclude 'main/gen/**'
    exclude 'test/**'
    // empty classpath
    classpath = files()
    //Failing the build
    ignoreFailures = false
}

tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

if (project.hasProperty("mainClass")) {
    mainClassName = mainClass
}

run {
    if (project.hasProperty('witness')) {
        standardInput = System.in
        args = ['--witness']
    }
}

def jdkVersion = JavaVersion.current().majorVersion.toInteger()
def debugAddress = jdkVersion >= 9 ? "*:5050" : "5050"

tasks.register('solidityNodeGroupTests', Test) {
    retry {
        maxRetries = 5
        maxFailures = 20
    }
    filter {
        includeTestsMatching "org.tron.core.services.filter.LiteFnQueryGrpcInterceptorTest"
        includeTestsMatching "org.tron.core.services.filter.LiteFnQueryHttpFilterTest"
        includeTestsMatching "org.tron.core.services.filter.RpcApiAccessInterceptorTest"
        includeTestsMatching "org.tron.core.services.http.BroadcastServletTest"
        includeTestsMatching "org.tron.core.services.http.ClearABIServletTest"
        includeTestsMatching "org.tron.core.services.http.CreateAccountServletTest"
        includeTestsMatching "org.tron.core.services.http.CreateAssetIssueServletTest"
        includeTestsMatching "org.tron.core.services.http.CreateSpendAuthSigServletTest"
        includeTestsMatching "org.tron.core.services.http.CreateWitnessServletTest"
        includeTestsMatching "org.tron.core.services.http.GetAccountByIdServletTest"
        includeTestsMatching "org.tron.core.services.http.GetAssetIssueListServletTest"
        includeTestsMatching "org.tron.core.services.http.GetBandwidthPricesServletTest"
        includeTestsMatching "org.tron.core.services.http.GetBlockByIdServletTest"
        includeTestsMatching "org.tron.core.services.http.GetBlockByNumServletTest"
        includeTestsMatching "org.tron.core.services.http.GetBrokerageServletTest"
        includeTestsMatching "org.tron.core.services.http.GetEnergyPricesServletTest"
        includeTestsMatching "org.tron.core.services.http.GetMemoFeePricesServletTest"
        includeTestsMatching "org.tron.core.services.http.GetNowBlockServletTest"
        includeTestsMatching "org.tron.core.services.http.GetRewardServletTest"
        includeTestsMatching "org.tron.core.services.http.GetTransactionInfoByBlockNumServletTest"
        includeTestsMatching "org.tron.core.services.http.GetTransactionInfoByIdServletTest"
        includeTestsMatching "org.tron.core.services.http.GetTransactionListFromPendingServletTest"
        includeTestsMatching "org.tron.core.services.http.HttpServletTest"
        includeTestsMatching "org.tron.core.services.http.JsonFormatTest"
        includeTestsMatching "org.tron.core.services.http.ListNodesServletTest"
        includeTestsMatching "org.tron.core.services.http.ListProposalsServletTest"
        includeTestsMatching "org.tron.core.services.http.TriggerSmartContractServletTest"
        includeTestsMatching "org.tron.core.services.http.UpdateAccountServletTest"
        includeTestsMatching "org.tron.core.services.http.UtilMockTest"
        includeTestsMatching "org.tron.core.services.http.UtilTest"
        includeTestsMatching "org.tron.core.services.http.solidity.GetTransactionByIdSolidityServletTest"
        includeTestsMatching "org.tron.core.services.interfaceOnPBFT.http.GetBandwidthPricesOnPBFTServletTest"
        includeTestsMatching "org.tron.core.services.interfaceOnPBFT.http.GetEnergyPricesOnPBFTServletTest"
        includeTestsMatching "org.tron.core.services.interfaceOnSolidity.http.GetBandwidthPricesOnSolidityServletTest"
        includeTestsMatching "org.tron.core.services.interfaceOnSolidity.http.GetEnergyPricesOnSolidityServletTest"
        includeTestsMatching "org.tron.core.services.jsonrpc.BlockResultTest"
        includeTestsMatching "org.tron.core.services.jsonrpc.BuildArgumentsTest"
        includeTestsMatching "org.tron.core.services.jsonrpc.CallArgumentsTest"
        includeTestsMatching "org.tron.core.services.jsonrpc.JsonRpcErrorResolverTest"
        includeTestsMatching "org.tron.core.services.jsonrpc.TransactionReceiptTest"
        includeTestsMatching "org.tron.core.services.jsonrpc.TransactionResultTest"
        includeTestsMatching "org.tron.core.services.ratelimiter.GlobalRateLimiterTest"
        includeTestsMatching "org.tron.core.services.ratelimiter.RuntimeDataTest"
        includeTestsMatching "org.tron.core.services.ratelimiter.adaptor.AdaptorTest"
        includeTestsMatching "org.tron.core.services.stop.BlockHeightStopTest"
        includeTestsMatching "org.tron.core.services.stop.BlockSyncCountStopTest"
        includeTestsMatching "org.tron.core.services.stop.BlockTimeStopTest"
        includeTestsMatching "org.tron.core.tire.TrieTest"
        includeTestsMatching "org.tron.core.witness.ProposalControllerTest"
        includeTestsMatching "org.tron.core.witness.WitnessControllerTest"
        includeTestsMatching "org.tron.core.zksnark.LibrustzcashTest"
        includeTestsMatching "org.tron.core.zksnark.MerkleContainerTest"
        includeTestsMatching "org.tron.core.zksnark.MerkleTreeTest"
        includeTestsMatching "org.tron.core.zksnark.NoteEncDecryTest"
        includeTestsMatching "org.tron.core.zksnark.SaplingNoteTest"
        includeTestsMatching "org.tron.core.zksnark.SendCoinShieldTest"
        includeTestsMatching "org.tron.core.zksnark.ShieldedReceiveTest"
        includeTestsMatching "org.tron.keystore.CredentialsTest"
        includeTestsMatching "org.tron.keystore.WalletFileTest"
        includeTestsMatching "org.tron.keystroe.CredentialsTest"
        includeTestsMatching "org.tron.program.AccountVoteWitnessTest"
        includeTestsMatching "org.tron.program.SolidityNodeTest"
    }

    exclude { element ->
        element.file.name.toLowerCase().contains('leveldb')
    }
    filter {
        excludeTestsMatching '*.*leveldb*'
        excludeTestsMatching '*.*Leveldb*'
        excludeTestsMatching '*.*LevelDB*'
        excludeTestsMatching '*.*LevelDb*'
    }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = false
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }

    maxHeapSize = "1024m"
    doFirst {
        // Restart the JVM after every 100 tests to avoid memory leaks and ensure test isolation
        forkEvery = 100
        jvmArgs "-XX:MetaspaceSize=128m","-XX:MaxMetaspaceSize=256m", "-XX:+UseG1GC" , "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=${debugAddress}"
    }
}

tasks.register('rpcApiServicesGroupTests', Test) {
    retry {
        maxRetries = 5
        maxFailures = 20
    }
    filter {
        includeTestsMatching "org.tron.core.net.MessageTest"
        includeTestsMatching "org.tron.core.net.NodeTest"
        includeTestsMatching "org.tron.core.net.P2pEventHandlerImplTest"
        includeTestsMatching "org.tron.core.net.P2pRateLimiterTest"
        includeTestsMatching "org.tron.core.net.TronNetDelegateTest"
        includeTestsMatching "org.tron.core.net.messagehandler.BlockMsgHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.ChainInventoryMsgHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.FetchInvDataMsgHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.InventoryMsgHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.MessageHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.PbftDataSyncHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.PbftMsgHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.SyncBlockChainMsgHandlerTest"
        includeTestsMatching "org.tron.core.net.messagehandler.TransactionsMsgHandlerTest"
        includeTestsMatching "org.tron.core.net.peer.PeerConnectionTest"
        includeTestsMatching "org.tron.core.net.peer.PeerManagerTest"
        includeTestsMatching "org.tron.core.net.peer.PeerStatusCheckMockTest"
        includeTestsMatching "org.tron.core.net.peer.PeerStatusCheckTest"
        includeTestsMatching "org.tron.core.net.service.nodepersist.DBNodeTest"
        includeTestsMatching "org.tron.core.net.services.AdvServiceTest"
        includeTestsMatching "org.tron.core.net.services.EffectiveCheckServiceTest"
        includeTestsMatching "org.tron.core.net.services.HandShakeServiceTest"
        includeTestsMatching "org.tron.core.net.services.RelayServiceTest"
        includeTestsMatching "org.tron.core.net.services.ResilienceServiceTest"
        includeTestsMatching "org.tron.core.net.services.SyncServiceTest"
        includeTestsMatching "org.tron.core.net.services.TronStatsManagerTest"
        includeTestsMatching "org.tron.core.pbft.PbftApiTest"
        includeTestsMatching "org.tron.core.pbft.PbftTest"
        includeTestsMatching "org.tron.core.services.RpcApiServicesTest"

    }

    exclude { element ->
        element.file.name.toLowerCase().contains('leveldb')
    }
    filter {
        excludeTestsMatching '*.*leveldb*'
        excludeTestsMatching '*.*Leveldb*'
        excludeTestsMatching '*.*LevelDB*'
        excludeTestsMatching '*.*LevelDb*'
    }


    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = false
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }

    maxHeapSize = "1024m"
    doFirst {
        // Restart the JVM after every 100 tests to avoid memory leaks and ensure test isolation
        forkEvery = 100
        jvmArgs "-XX:MetaspaceSize=128m","-XX:MaxMetaspaceSize=256m", "-XX:+UseG1GC" , "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=${debugAddress}"
    }
}

test {
    retry {
        maxRetries = 5
        maxFailures = 20
    }
    testLogging {
        exceptionFormat = 'full'
      //  showStandardStreams = true
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    if (rootProject.archInfo.isArm64) {
    exclude { element ->
        element.file.name.toLowerCase().contains('leveldb')
    }
    filter {
        excludeTestsMatching '*.*leveldb*'
        excludeTestsMatching '*.*Leveldb*'
        excludeTestsMatching '*.*LevelDB*'
        excludeTestsMatching '*.*LevelDb*'
    }
    }
    if (isWindows()) {
        exclude '**/ShieldedTransferActuatorTest.class'
        exclude '**/BackupDbUtilTest.class'
        exclude '**/ManagerTest.class'
        exclude 'org/tron/core/zksnark/**'
        exclude 'org/tron/common/runtime/vm/PrecompiledContractsVerifyProofTest.class'
        exclude 'org/tron/core/ShieldedTRC20BuilderTest.class'
        exclude 'org/tron/common/runtime/vm/WithdrawRewardTest.class'
    }
    maxHeapSize = "1024m"
    doFirst {
        // Restart the JVM after every 100 tests to avoid memory leaks and ensure test isolation
        forkEvery = 100
       // jvmArgs "-XX:MetaspaceSize=128m","-XX:MaxMetaspaceSize=256m", "-XX:+UseG1GC" , "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=${debugAddress}"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
    getExecutionData().setFrom(fileTree('../framework/build/jacoco').include("**.exec"))
}

def binaryRelease(taskName, jarName, mainClass) {
    return tasks.create("${taskName}", Jar) {
        baseName = jarName
        version = null
        from(sourceSets.main.output) {
            include "/**"
        }

        // explicit_dependency
        dependsOn (project(':actuator').jar, project(':consensus').jar, project(':chainbase').jar,
                project(':crypto').jar, project(':common').jar, project(':protocol').jar, project(':platform').jar)

        from {
            configurations.runtimeClasspath.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        // exclude these files for bouncycastle
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"

        manifest {
            attributes "Main-Class": "${mainClass}"
        }

//        classifier(jarName)
    }
}

def createScript(project, mainClass, name) {
    project.tasks.create(name: name, type: CreateStartScripts) {
        unixStartScriptGenerator.template = resources.text.fromFile('../gradle/unixStartScript.txt')
        windowsStartScriptGenerator.template = resources.text.fromFile('../gradle/windowsStartScript.txt')
        outputDir = new File(project.buildDir, 'scripts')
        mainClassName = mainClass
        applicationName = name
        classpath = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtimeClasspath
//        defaultJvmOpts = ['-XX:+UseConcMarkSweepGC',
//                          '-XX:+PrintGCDetails',
//                          '-Xloggc:./gc.log',
//                          '-XX:+PrintGCDateStamps',
//                          '-XX:+CMSParallelRemarkEnabled',
//                          '-XX:ReservedCodeCacheSize=256m',
//                          '-XX:+CMSScavengeBeforeRemark'
//        ]
    }
    project.tasks[name].dependsOn(project.jar)
    project.applicationDistribution.with {
        into("bin") {
            from(project.tasks[name])
            fileMode = 0755
        }
    }
}
applicationDistribution.from(rootProject.archInfo.VMOptions) {
    into "bin"
}
//distZip {
//    doLast {
//        file("$destinationDir/$archiveName").renameTo("$destinationDir/"+'java-tron-'+version+'-bin.zip')
//    }
//}
configurations.archives.artifacts.removeAll { it.type == 'tar' }
applicationName = 'java-tron'
startScripts.enabled = false
run.enabled = false
tasks.distTar.enabled = false

createScript(project, 'org.tron.program.FullNode', 'FullNode')
def releaseBinary = hasProperty('binaryRelease') ? getProperty('binaryRelease') : 'true'
if (releaseBinary == 'true') {
    artifacts {
        archives(binaryRelease('buildFullNodeJar', 'FullNode', 'org.tron.program.FullNode'))
    }
}

task copyToParent(type: Copy) {
    into "../build/distributions"
    from "$buildDir/distributions"
    include "*.zip"
}

build.finalizedBy(copyToParent)
