
plugins {
    // Provide convenience executables for trying out the examples.
    // id 'application'
    // ASSUMES GRADLE 5.6 OR HIGHER. Use plugin version 0.8.10 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.12'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
}

def protobufVersion = "3.19.2"

def grpcVersion = "1.48.1"
def protocVersion = protobufVersion

dependencies {
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion
    compile group: 'net.jcip', name: 'jcip-annotations', version: '1.0'

//    checkstyleConfig "com.puppycrawl.tools:checkstyle:${versions.checkstyle}"
    // google grpc
    compile group: 'io.grpc', name: 'grpc-netty', version: grpcVersion
    compile group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
    compile group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
    // end google grpc

    compile group: 'com.google.api.grpc', name: 'googleapis-common-protos', version: '0.0.3'
}

tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/protos'
        }
        java {
            srcDir 'src/main/gen'
            srcDir 'src/main/java'
        }
    }

}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/"
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { outputSubDir = "java" }
            }
        }
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }
    }
}

clean.doFirst {
    delete "src/main/java"
}

